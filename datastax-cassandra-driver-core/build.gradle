apply from: "../gradle/shared.gradle"

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1' }
}

apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.palantir.shaded.com.datastax.cassandra'

dependencies {
    compile(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '2.1.5') {
        exclude(module:"snappy-java")
        exclude(group:'org.slf4j')
    }
}

shadowJar {
    mergeServiceFiles()
    relocate 'com.google', 'com.datastax.palantirshaded.google'
    relocate 'com.codahale.metrics', 'com.datastax.palantirshaded.metrics'
    relocate 'org.jboss.netty', 'com.datastax.shaded.netty'
    from "NOTICE"
}

publishing {
    publications {
        bintray(MavenPublication) {
            artifact(shadowJar)
            pom.withXml {
                def node = asNode()
                node.remove(node.get('licenses'))
            }
        }
    }
}
